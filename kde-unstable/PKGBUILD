# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=markdownpart
pkgver=22.11.80
pkgrel=1
pkgdesc='KPart for rendering Markdown content'
arch=(x86_64)
url='https://apps.kde.org/markdownpart/'
license=(GPL)
depends=(kparts)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('46180490ca6652e007c725db26e491520577c4cc3aacde4b962e7bac50a11234'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
