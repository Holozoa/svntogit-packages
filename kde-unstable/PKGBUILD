# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=zanshin
pkgver=22.07.90
pkgrel=1
pkgdesc='To-do management application based on Akonadi'
arch=(x86_64)
url='https://zanshin.kde.org'
license=(GPL)
depends=(kdepim-runtime kontactinterface)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kde-pim)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('904254ea13afd376c379f9c81496ea4289eb61bfac81657dbc859155d983f357'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
