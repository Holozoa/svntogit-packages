# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kget
pkgver=22.03.80
pkgrel=1
pkgdesc='Download Manager'
url='https://apps.kde.org/kget/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-network)
depends=(qca-qt5 qgpgme plasma-workspace)
makedepends=(extra-cmake-modules boost libktorrent libmms kdoctools)
optdepends=('libktorrent: torrent support' 'libmms: MMS support')
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        kget-sqlite.patch)
sha256sums=('bd77fcac40708681ce534c19c3a82f536142c4b5e7fa6ba92d5ab390a3c513d2'
            'SKIP'
            'c6f626869fcdd42b6ebe8453f4516368a653c8f5e29a4db2e9f0d93d4411b833')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < kget-sqlite.patch # Fix sqlite detection
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
