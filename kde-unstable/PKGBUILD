# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmag
pkgver=22.03.80
pkgrel=2
arch=(x86_64)
pkgdesc='Screen Magnifier'
url='https://apps.kde.org/kmag/'
license=(GPL FDL)
groups=(kde-applications kde-accessibility)
depends=(kio libqaccessibilityclient hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/accessibility/kmag/-/commit/ae1ddd8b.patch)
sha256sums=('fcbf4cbca12758eb19d3b9be39cf22572b8f5d95af33a3bc013dc8a56deca5ce'
            'SKIP'
            '713d2b5f142dd0bd4b558f24ca72a6f72a1c3209e91f2095fb5f51c47a60adfa')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -Rp1 < ae1ddd8b.patch # Use an actually existing qaccessibility version
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
