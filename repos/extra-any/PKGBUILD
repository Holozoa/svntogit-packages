# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.3.2
pkgrel=2
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=('any')
url="https://github.com/archlinux/archinstall"
license=('GPL3')
depends=('python')
makedepends=('python-setuptools' 'python-sphinx')
provides=('python-archinstall')
conflicts=('python-archinstall')
replaces=('python-archinstall')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig")
sha512sums=('47313863846ed8d45d5a38c256ea055f1fdc3307ebf7ebf61fef6d3f4d0c1e989a377d47eff0975e1a56158819fca95538def5d7620222b27d951a981845d95d'
            'SKIP')
b2sums=('145343e3e827e239f45f7ce3a1255d6d6ece0854d31d0bca8a81a914d50d13c27a7bd8a31be51f659d2c0ce213b0ae56beff244ccea073b15632be44f5c0b70f'
        'SKIP')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A') # Anton Hvornum (Torxed) <anton@hvornum.se>


build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="${pkgdir}" --optimize=1
  install -vDm 644 docs/_build/man/archinstall.1 -t "${pkgdir}/usr/share/man/man1/"
}
