# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Arvid Ephraim Picciani <aep@exys.org>
# Contributor: Michael Krauss <hippodriver@gmx.net>

pkgname=python-pyparsing
pkgver=3.0.4
pkgrel=1
pkgdesc='General parsing module for Python'
arch=('any')
url='https://github.com/pyparsing/pyparsing/'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-jinja' 'python-railroad-diagrams')
optdepends=('python-railroad-diagrams: for generating Railroad Diagrams'
            'python-jinja: for generating Railroad Diagrams')
source=("https://github.com/pyparsing/pyparsing/archive/pyparsing_$pkgver.tar.gz")
sha512sums=('01acf9b7663feae57e82ac3139c35687ea2754874425acfcf29b82595977660dab86ec35665e96400eb92acfbb51fb2326e010b960be7570de7575296078bd31')

build() {
  cd pyparsing-pyparsing_$pkgver
  python setup.py build
}

check() {
  cd pyparsing-pyparsing_$pkgver
  python -m unittest
}

package() {
  cd pyparsing-pyparsing_$pkgver

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
