# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-date-manip
pkgver=6.87
pkgrel=1
pkgdesc="Date::Manip - date manipulation routines"
arch=('any')
license=('PerlArtistic')
url="https://search.cpan.org/dist/Date-Manip/"
depends=('perl')
makedepends=('perl-file-find-rule' 'perl-test-inter' 'perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/S/SB/SBECK/Date-Manip-$pkgver.tar.gz")
sha512sums=('f1d03b272881b1af23c1899446ad497e0c4ef301bcf5e46fad71737e3ee9d6ba8f08250939e6f4b4de10031f71bee2389a0b95190099225ce43c1bd42a85714e')

build() {
  cd Date-Manip-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Date-Manip-$pkgver
  make test
}

package() {
  cd Date-Manip-$pkgver
  make DESTDIR="$pkgdir" install
}
