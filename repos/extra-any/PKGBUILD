# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Arvid Ephraim Picciani <aep@exys.org>
# Contributor: Michael Krauss <hippodriver@gmx.net>

pkgname=python-pyparsing
pkgver=3.0.1
pkgrel=1
pkgdesc='General parsing module for Python'
arch=('any')
url='https://github.com/pyparsing/pyparsing/'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-jinja' 'python-railroad-diagrams')
optdepends=('python-railroad-diagrams: for generating Railroad Diagrams'
            'python-jinja: for generating Railroad Diagrams')
source=("https://github.com/pyparsing/pyparsing/archive/pyparsing_$pkgver.tar.gz")
sha512sums=('70f2f957c713a88979ae95de8834a79c777196a2a6d47a25bf930cdefdc3120919147a33119ada0d33c53bb215fcddfa969b0d1630d2979c9e685d85e5c57414')

build() {
  cd pyparsing-pyparsing_$pkgver
  python setup.py build
}

check() {
  cd pyparsing-pyparsing_$pkgver
  python -m unittest
}

package() {
  cd pyparsing-pyparsing_$pkgver

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
