# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Generator : pbjam 0.01

pkgname=perl-http-message
pkgver=6.38
pkgrel=1
pkgdesc="HTTP style messages"
arch=('any')
url='https://search.cpan.org/dist/HTTP-Message'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-encode-locale' 'perl-http-date' 'perl-io-html' 'perl-lwp-mediatypes'
         'perl-uri')
checkdepends=('perl-io-compress-brotli' 'perl-test-needs' 'perl-try-tiny')
conflicts=('perl-libwww<6')
source=("https://search.cpan.org/CPAN/authors/id/O/OA/OALDERS/HTTP-Message-$pkgver.tar.gz")
sha512sums=('44731e7db9f73b936c346162ba8c59322d3ee14b5099fb8b7be787b18732296a0615c99a04de8a36e2f1931fe8052797ce218dd81ef2869fe917d610e339b1c0')

build() {
  cd HTTP-Message-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd HTTP-Message-$pkgver
  make test
}

package() {
  cd HTTP-Message-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
