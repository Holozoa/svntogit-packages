# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=xorg-xclock
pkgver=1.1.0
pkgrel=1
pkgdesc="X clock"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu' 'libxaw' 'libxrender' 'libxft' 'libxkbfile')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xclock-${pkgver}.tar.xz{,.sig})
sha512sums=('9456ed43cb8afce4a1f1c4edcb87bfb86f61fb54d1892f26812ed10662c9b8f8c28854934ce4eb981090d500fec0f295ba96df9867dd1685dc5f31202729a1a0'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd "xclock-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "xclock-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
