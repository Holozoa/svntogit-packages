# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libappimage
pkgver=1.0.4.3
_pkgver=1.0.4-3
pkgrel=1
pkgdesc='Reference implementation of the AppImage specification'
arch=(x86_64)
url='https://appimage.org/'
license=(GPL)
depends=(libarchive squashfuse boost-libs librsvg)
makedepends=(cmake wget vim desktop-file-utils boost xdg-utils-cxx)
source=(https://github.com/AppImage/libappimage/archive/v$_pkgver/$pkgname-$_pkgver.tar.gz)
sha256sums=('3ed38d08338b66137cf56a3ba3cdd3ef26dfa75dfa6ca02e7faa74f7683dbc12')

build() {
  cmake -B build -S $pkgname-$_pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
    -DUSE_SYSTEM_XZ=ON \
    -DUSE_SYSTEM_SQUASHFUSE=ON \
    -DUSE_SYSTEM_LIBARCHIVE=ON \
    -DUSE_SYSTEM_BOOST=ON \
    -DUSE_SYSTEM_XDGUTILS=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
