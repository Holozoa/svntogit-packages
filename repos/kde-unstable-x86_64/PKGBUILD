# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=filelight
pkgver=22.07.90
pkgrel=1
pkgdesc='View disk usage information'
url='https://apps.kde.org/filelight/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kdeclarative qqc2-desktop-style kquickcharts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0beb50f9fe95ff88fc7a316f32cafa9b8a245f27aca9ca5716658a315acbf313'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
