# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kidentitymanagement
pkgver=22.03.80
pkgrel=1
pkgdesc='KDE PIM libraries'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kpimtextedit)
makedepends=(extra-cmake-modules doxygen qt5-tools)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f8e2927bcb9836d4e83b5959bc53404fc8864644725c28ad960a2a0154e491a1'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
