# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=print-manager
pkgver=22.03.80
pkgrel=2
pkgdesc='A tool for managing print jobs and printers'
arch=(x86_64)
url='https://apps.kde.org/print-manager/'
license=(GPL LGPL FDL)
depends=(kcmutils kirigami2)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
optdepends=('system-config-printer: auto-detect the printer driver')
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/utilities/print-manager/-/commit/1abb32b7.patch)
sha256sums=('43fabdd67349b1fcca5e976c9bb50708cc5d92934436ecd2bc6b79590a032808'
            'SKIP'
            '3bf1ec4b26b3c9b8b22f21e3632c0d8ec513f2b5defa926a25d1e3988a18a592')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < 1abb32b7.patch # Fix applet loading
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
