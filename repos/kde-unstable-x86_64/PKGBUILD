# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt6-multimedia
_qtver=6.4.0-beta1
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for audio, video, radio and camera functionality'
depends=(qt6-base gst-plugins-base)
makedepends=(cmake ninja qt6-declarative qt6-shadertools libpulse ffmpeg qt6-quick3d)
optdepends=('qt6-declarative: QML bindings'
            'qt6-quick3d: for QtQuick3DAudio'
            'gst-plugins-good: camera support, additional plugins'
            'gst-plugins-bad: camera support, additional plugins'
            'gst-plugins-ugly: additional plugins'
            'gst-libav: additional plugins'
            'ffmpeg: FFmpeg backend'
            'libva: FFmpeg backend')
groups=(qt6)
options=(debug)
_pkgfn=${pkgname/6-/}-everywhere-src-$_qtver
source=(https://download.qt.io/development_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz)
sha256sums=('54fda3e0bdecefb75058331ad55b8888787ef19c0442da67b3a4c15215533b32')

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
