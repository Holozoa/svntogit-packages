# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=krfb
pkgver=22.07.90
pkgrel=1
pkgdesc='Desktop Sharing'
url='https://apps.kde.org/krfb/'
arch=(x86_64)
license=(LGPL)
depends=(kdnssd kxmlgui kwallet libvncserver kcompletion kwayland xdg-desktop-portal)
makedepends=(extra-cmake-modules kdoctools plasma-wayland-protocols)
groups=(kde-applications kde-network)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8a655d1b6cd1881070b8ad2102af686da4773daa4c758c337cf84f89c4632a48'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
