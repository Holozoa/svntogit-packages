# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgbase=nvidia-open
pkgname=(nvidia-open nvidia-open-dkms)
pkgver=515.43.04
pkgrel=1
pkgdesc="NVIDIA open kernel modules"
arch=('x86_64')
url="https://github.com/NVIDIA/open-gpu-kernel-modules"
license=('GPL')
options=('!lto')
install=nvidia-open.install
makedepends=('linux-headers')
source=("$pkgname-$pkgver.tar.gz::https://github.com/NVIDIA/open-gpu-kernel-modules/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('2a95132cfebe2dd746e2b507829618557a2518b56d8d77628df9f8073f154fab769604b0789e852e4cc6a52f3130b4b637068b0469931464019f8386db48041f')

prepare() {
  cd open-gpu-kernel-modules-${pkgver}
  # Attempt to make this reproducible
  sed -i "s/^HOSTNAME.*/HOSTNAME = echo archlinux"/ utils.mk
  sed -i "s/^WHOAMI.*/WHOAMI = echo archlinux-builder"/ utils.mk
  sed -i "s/^DATE.*/DATE = date -r version.mk"/ utils.mk

  sed -i "s/__VERSION_STRING/${pkgver}/" kernel-open/dkms.conf
  sed -i 's/__JOBS/`nproc`/' kernel-open/dkms.conf
  sed -i 's/__EXCLUDE_MODULES//' kernel-open/dkms.conf
  sed -i 's/__DKMS_MODULES//' kernel-open/dkms.conf
  sed -i '$iBUILT_MODULE_NAME[0]="kernel-open/nvidia"\
DEST_MODULE_LOCATION[0]="/kernel/drivers/video"\
BUILT_MODULE_NAME[1]="kernel-open/nvidia-uvm"\
DEST_MODULE_LOCATION[1]="/kernel/drivers/video"\
BUILT_MODULE_NAME[2]="kernel-open/nvidia-modeset"\
DEST_MODULE_LOCATION[2]="/kernel/drivers/video"\
BUILT_MODULE_NAME[3]="kernel-open/nvidia-drm"\
DEST_MODULE_LOCATION[3]="/kernel/drivers/video"\
BUILT_MODULE_NAME[4]="kernel-open/nvidia-peermem"\
DEST_MODULE_LOCATION[4]="/kernel/drivers/video"' kernel-open/dkms.conf

  # Clean version for later copying for DKMS
  cp -r ../open-gpu-kernel-modules-${pkgver} "$srcdir"/open-gpu-kernel-modules-dkms
}

build() {
  cd open-gpu-kernel-modules-${pkgver}
  make SYSSRC="/usr/src/linux" CONDITIONAL_CFLAGS=" -mindirect-branch-cs-prefix "
}

package_nvidia-open() {
  depends=('linux')
  conflicts=('NVIDIA-MODULE')
  provides=('NVIDIA-MODULE')

  cd open-gpu-kernel-modules-${pkgver}
  _extradir="/usr/lib/modules/$(</usr/src/linux/version)/extramodules"
  install -Dt "${pkgdir}${_extradir}" -m644 kernel-open/*.ko
  find "${pkgdir}" -name '*.ko' -exec strip --strip-debug {} +
  find "${pkgdir}" -name '*.ko' -exec xz {} +

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname
}

package_nvidia-open-dkms() {
  depends=('dkms')
  conflicts=('nvidia-open' 'NVIDIA-MODULE')
  provides=('nvidia-open' 'NVIDIA-MODULE')

  install -dm 755 "${pkgdir}"/usr/src
  # cp -dr --no-preserve='ownership' kernel-open "${pkgdir}/usr/src/nvidia-$pkgver"
  cp -dr --no-preserve='ownership' open-gpu-kernel-modules-dkms "${pkgdir}/usr/src/nvidia-$pkgver"
  mv "${pkgdir}/usr/src/nvidia-$pkgver/kernel-open/dkms.conf" "${pkgdir}/usr/src/nvidia-$pkgver/dkms.conf"

  install -Dm644 open-gpu-kernel-modules-${pkgver}/COPYING "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set sw=2 et:
