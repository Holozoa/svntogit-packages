# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: tobias <tobias@archlinux.org>

pkgname=inkscape
pkgver=1.1.2
_tag='0a00cf5339ab09436cbe123d22e98fb791619733' # git rev-parse INKSCAPE_${pkgver/./_}
pkgrel=3
pkgdesc='Professional vector graphics editor'
url='https://inkscape.org/'
license=('GPL' 'LGPL')
arch=('x86_64')
makedepends=('cmake' 'boost' 'git')

depends=(
	'dbus-glib'
	'double-conversion'
	'gc'
	'graphicsmagick'
	'gsl'
	'gspell'
	'gtkmm3'
	'lcms2'
	'lib2geom'
	'libcdr'
	'libjpeg-turbo'
	'libvisio'
	'libxslt'
	'poppler-glib'
	'potrace'
	'python'
	'ttf-font'
)

optdepends=(
	'fig2dev: xfig input'
	'gvfs: import clip art'
	'pstoedit: latex formulas'
	'python-lxml: some extensions'
	'python-numpy: some extensions'
	'scour: optimized SVG output, some extensions'
	'texlive-core: latex formulas'
)

#source=("https://media.inkscape.org/dl/resources/file/${pkgname}-${pkgver}.tar.bz2")
source=("git+https://gitlab.com/inkscape/inkscape.git#tag=${_tag}")
sha256sums=('SKIP')

_backports=(
  # Ensure compatibility with Poppler 22.03 and later
  'a18c57ffff313fd08bc8a44f6b6bf0b01d7e9b75'

  # Compatibility with Poppler 22.04
  'd989cdf1059c78bc3bb6414330242073768d640b'
)

prepare() {
	cd "${pkgname}"
	git submodule init
	git submodule update

	local _c
	for _c in "${_backports[@]}"; do
		git log --oneline -1 "${_c}"
		git cherry-pick -n "${_c}"
	done
}

build() {
	mkdir -p build
	export PKG_CONFIG_PATH='/usr/lib/imagemagick6/pkgconfig'
	cmake -H${pkgname} -Bbuild \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DWITH_DBUS=ON \

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --build build --target install
}
