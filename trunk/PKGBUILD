# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tinywrkb <tinywrkb@gmail.com>

pkgname=power-profiles-daemon
pkgver=0.11
pkgrel=1
pkgdesc='Makes power profiles handling available over D-Bus'
url='https://gitlab.freedesktop.org/hadess/power-profiles-daemon'
license=(GPL3)
arch=(x86_64)
depends=(upower polkit)
optdepends=('python-gobject: for powerprofilesctl')
makedepends=(meson)
checkdepends=(python-dbusmock python-isort python-mccabe umockdev)
source=(https://gitlab.freedesktop.org/hadess/power-profiles-daemon/-/archive/$pkgver/$pkgname-$pkgver.tar.gz)
b2sums=('12a7b31739355f707af4d694c5a9d44250d0b4ef2d994a4b95cdbf99e377ea95ed488310ae8f7a0a14a67caeba442fb98990af112234f27bbb63d4ec91a74417')

build() {
  meson $pkgname-$pkgver build \
    --prefix /usr \
    --libexec lib \
    --sysconfdir /usr/share
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
