# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Massimiliano Torromeo < massimiliano DOT torromeo AT gmail DOT com >

pkgname=python-asn1crypto
pkgver=1.5.0
pkgrel=1
arch=('any')
license=('MIT')
pkgdesc="Python ASN.1 library with a focus on performance and a pythonic API"
url="https://github.com/wbond/asn1crypto"
depends=('python')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wbond/asn1crypto/archive/$pkgver.tar.gz")
sha512sums=('2045ae0ddf777c7a6bc6ba946f457e6a2561d4ebe8fa0ad5b6663af345c6a232a4fd1751dcad6e3c4a99b56c06148801485fb81f1a48e04b1b711dda9d6d0e54')

build() {
  cd "$srcdir"/asn1crypto-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/asn1crypto-$pkgver
  python run.py tests
}

package() {
  cd asn1crypto-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/python-asn1crypto/LICENSE
}
