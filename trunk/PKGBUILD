# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libnma
pkgver=1.8.36
pkgrel=1
pkgdesc="NetworkManager GUI client library"
url="https://gitlab.gnome.org/GNOME/libnma"
arch=(x86_64)
license=(GPL LGPL)
depends=(libnm gcr gtk3 iso-codes mobile-broadband-provider-info)
makedepends=(gobject-introspection git gtk-doc meson vala)
provides=(libnma.so)
options=(debug)
_commit=d3080c86c6cfb71d16514af4d967d36dba976206  # tags/1.8.36^0
source=("git+https://gitlab.gnome.org/GNOME/libnma.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libnma
  git describe --tags | sed 's/-dev/dev/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libnma
}

build() {
  arch-meson libnma build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
