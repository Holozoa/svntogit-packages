# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=0.99.16
pkgrel=2
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
url="https://upower.freedesktop.org"
arch=(x86_64)
license=(GPL)
depends=(systemd libimobiledevice libgudev)
makedepends=(docbook-xsl gobject-introspection python git gtk-doc meson)
checkdepends=(python-{dbus,dbusmock,gobject} umockdev)
backup=(etc/UPower/UPower.conf)
_commit=2e5ba3ef806fab1028c5030ef630f66e29b4366d  # tags/v0.99.16^0
source=("git+https://gitlab.freedesktop.org/upower/upower.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd upower
  git describe --tags | sed -e 's/^v\|^UPOWER_//;s/_/\./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd upower

  # https://bugs.archlinux.org/task/73942
  git cherry-pick -n 7192ff481bc4 72469426b737 2f81d690148c
}

build() {
  arch-meson upower build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so)
  provides+=(libupower-glib.so)

  meson install -C build --destdir "$pkgdir"
}
