# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Arjan Timmerman <arjan.archlinux.org>
# Contributor: Tom Newsom <Jeepster.gmx.co.uk>

pkgname=imlib2
pkgver=1.9.0
pkgrel=2
pkgdesc='Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support'
url='https://sourceforge.net/projects/enlightenment/'
arch=('x86_64')
license=('BSD')
options=('debug')
# Currently highway does provide a static library only, that libjxl links to.
# This introduces a build dependency for now...
makedepends=('highway')
depends=('bzip2' 'freetype2' 'giflib' 'libheif' 'libid3tag' 'libjpeg-turbo' 'libjxl'
         'libpng' 'librsvg' 'libspectre' 'libtiff' 'libwebp' 'libxext' 'openjpeg2' 'xz')
source=("https://downloads.sourceforge.net/project/enlightenment/imlib2-src/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('958fdabc938ec03ab48001ca4df72c8352cb261b12b09334d38348b22b8859cff16a1cda0d4447b73261723f223f62a799f80eb85a710ac5a44a3fa4f90734fd')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/imlib2 \
    --x-libraries=/usr/lib \
    --enable-amd64
  make
}


check() {
  cd "${pkgname}-${pkgver}"
  make check
}


package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install License
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
