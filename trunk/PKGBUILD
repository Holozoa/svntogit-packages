# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: David Herrmann <dh.herrmann@gmail.com>

pkgname=dbus-broker
pkgver=30
pkgrel=2
pkgdesc="Linux D-Bus Message Broker"
url="https://github.com/bus1/dbus-broker/wiki"
arch=(x86_64)
license=(Apache)
depends=(systemd-libs expat audit)
makedepends=(meson systemd python-docutils)
options=(debug)
source=(https://github.com/bus1/dbus-broker/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz
        0001-util-user-keep-reference-to-user-in-each-usage-table.patch)
sha256sums=('bf22ba6a13680ba93b99f0bccb54dde9f4f6bdff5f298dbc8cdb067a80f3827a'
            'e7107b54a0051e3945cfde3a8cfc617abcef63b38dd0c5783ba805fedab9e2d1')

prepare() {
  cd $pkgname-$pkgver

  # Fix asserts in system bus
  patch -Np1 -i ../0001-util-user-keep-reference-to-user-in-each-usage-table.patch
}

build() {
  arch-meson $pkgname-$pkgver build \
    -D audit=true \
    -D docs=true \
    -D linux-4-17=true \
    -D system-console-users=gdm,sddm,lightdm,lxdm
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
