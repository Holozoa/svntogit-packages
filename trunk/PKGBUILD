# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kajongg
pkgver=22.04.0
pkgrel=2
pkgdesc='The ancient Chinese board game for 4 players'
url='https://apps.kde.org/kajongg/'
arch=(any)
license=(GPL LGPL FDL)
groups=(kde-applications kde-games)
depends=(python-twisted python-qtpy qt5-python-bindings libkmahjongg hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('vorbis-tools: for playing sounds')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/games/kajongg/-/commit/75cf1656.patch)
sha256sums=('d51297cde41d81a8d289dc91ac464bd4dfe2f87923858212c76e34bcc32ca9cc'
            'SKIP'
            '309380e2ce7e36da2c3c0313232b7a6efffe072991790f4bb16433bd6d9193c3')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 75cf1656.patch # Install missing file
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
